/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Blocks.Gallery;

use Runtime.Widget.Dialog.BaseDialogModel;
use Runtime.Widget.Blocks.Gallery.GalleryDialog;


class GalleryDialogModel extends BaseDialogModel
{
	string component = classof GalleryDialog;
	Collection<Dict> items = [];
	bool big_image_contain = false;
	int current = 0;
	
	
	/**
	 * Returns current image
	 */
	string getCurrentImage()
	{
		Dict item = this.items.get(this.current);
		if (not item) return "";
		if (not item.has("big")) return "";
		return item.get("big");
	}
	
	
	/**
	 * Select image
	 */
	void select(int pos)
	{
		int count = this.items.count();
		if (count == 0)
		{
			this.current = 0;
			return;
		}
		this.current = pos % count;
		if (this.current < 0)
		{
			this.current = (this.current + count) % count;
		}
	}
	
	
	/**
	 * Show prev image
	 */
	void prev()
	{
		this.select(this.current - 1);
	}
	
	
	/**
	 * Show next image
	 */
	void next()
	{
		this.select(this.current + 1);
	}
}